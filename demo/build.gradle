apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile file("/Users/Blundell/Dropbox/Android/keystore.txt")
            keyAlias "blundell"
            keyPassword "placeholder"
            storePassword "placeholder"
        }
    }
    compileSdkVersion 20
    buildToolsVersion '20.0.0'
    defaultConfig {
        applicationId "com.blundell.fpsdoug.free"
        minSdkVersion 14
        targetSdkVersion 20
        versionCode 11
        versionName "3.0.3"
    }
    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':soundboardEngine')
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("Keystore password: "))
    def keyPw = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}
